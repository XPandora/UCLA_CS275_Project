#pragma kernel CSMain
static const int threadGroupSize = 1024;

struct FishDataBuffer {
    int type; // 0: prey, 1: predator, 2: pacifist
    float3 position;
    float3 direction;

    float3 flockHeading;
    float3 flockCentre;
    float3 separationHeading;
    int numFlockmates;

    float totFear;
    float maxFear;
    float threat; // 0 for prey 1 for predator

     // eat
    float3 nearestPredatorPos;
    int eatenPreyNumber;
    // mate
    int intention;
    int fishSex;
    float size;
    int correct_like;

    int desiredMateID;
    float3 desiredMatePos;
    int desiredMateIntention;
};

RWStructuredBuffer<FishDataBuffer> fishDatBuffer;
int numFishes;
float viewRadius;
float flockRadius;
float avoidRadius;
float canEatRange;

[numthreads(threadGroupSize,1,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    float D0 = 1000000;
    float tmpMaxThreat = 0;
    float tempNearestDistSqr = 1000000000.0;

    int tempDesiredID = -1;
    float tempNearestDistMateSqr = 1000000000.0;
    float tempNearLargestMaleMateSize = 0;
    fishDatBuffer[id.x].correct_like = 0;

    for (int idxNeighbor = 0; idxNeighbor < numFishes; idxNeighbor ++) {
        if (id.x != idxNeighbor) {
            FishDataBuffer neighbor = fishDatBuffer[idxNeighbor];
            int neighbor_type = neighbor.type;
            
            float3 offset = neighbor.position - fishDatBuffer[id.x].position;
            float sqrDst = offset.x * offset.x + offset.y * offset.y + offset.z * offset.z;

            // assume all fish types share same view radius and avoid raduis for temporary use
            if (sqrDst < viewRadius * viewRadius) {
                if (fishDatBuffer[id.x].type == 0) // prey
                {
                    if (neighbor_type == 0 && sqrDst < flockRadius * flockRadius) // prey neighbor
                    {
                        fishDatBuffer[id.x].numFlockmates += 1;
                        fishDatBuffer[id.x].flockHeading += neighbor.direction;
                        fishDatBuffer[id.x].flockCentre += neighbor.position;

                        if (sqrDst < avoidRadius * avoidRadius) {
                            fishDatBuffer[id.x].separationHeading -= offset / sqrDst;
                        }
                    }
                    else if (neighbor_type == 1) // predator neighbor
                    {
                        // accumulate fear
                        fishDatBuffer[id.x].totFear += D0 / sqrt(sqrDst);
                        tmpMaxThreat = max(tmpMaxThreat, D0 / sqrt(sqrDst));

                        // get nearestPredatorPos
                        if(sqrDst < tempNearestDistSqr){
                            tempNearestDistSqr = sqrDst;
                            fishDatBuffer[id.x].nearestPredatorPos = fishDatBuffer[idxNeighbor].position;
                        }
                        // TODO: update focusser info
                        // TODO: after loop neighbors, if tempNearestDistSqr is really small, self is eaten
                    }

                    // no interest on pacifist neighbor
                }
                else if (fishDatBuffer[id.x].type == 1) // predator
                {
                    // only have interest on prey neighbor
                    if (neighbor_type == 0)
                    {
                        // TODO: if very very close, eatenPreyNumber++
                        if(sqrDst < canEatRange * canEatRange){
                            fishDatBuffer[id.x].eatenPreyNumber += 1;
                        }
                        
                    }
                }
                else // pacifist
                {
                    // only have interest on pacifist neighbor
                    if (neighbor_type == 2)
                    {
                        // only look for different sex
                        if(fishDatBuffer[id.x].fishSex != fishDatBuffer[idxNeighbor].fishSex){
                            if(fishDatBuffer[id.x].fishSex == 0){
                                // male
                                if(sqrDst < tempNearestDistMateSqr){
                                    tempNearestDistMateSqr = sqrDst;
                                    tempDesiredID = idxNeighbor;
                                    fishDatBuffer[id.x].desiredMatePos = fishDatBuffer[idxNeighbor].position;
                                    fishDatBuffer[id.x].desiredMateIntention = fishDatBuffer[idxNeighbor].intention;
                                    if(id.x == fishDatBuffer[idxNeighbor].desiredMateID)
                                        fishDatBuffer[id.x].correct_like = 1;
                                    else
                                        fishDatBuffer[id.x].correct_like = 0;
                                }
                            }
                            else{
                                // female
                                if(fishDatBuffer[idxNeighbor].size > tempNearLargestMaleMateSize){
                                    tempNearLargestMaleMateSize = fishDatBuffer[idxNeighbor].size;
                                    tempDesiredID = idxNeighbor;
                                    fishDatBuffer[id.x].desiredMatePos = fishDatBuffer[idxNeighbor].position;
                                    fishDatBuffer[id.x].desiredMateIntention = fishDatBuffer[idxNeighbor].intention;
                                    if(id.x == fishDatBuffer[idxNeighbor].desiredMateID)
                                        fishDatBuffer[id.x].correct_like = 1;
                                    else
                                        fishDatBuffer[id.x].correct_like = 0;
                                }
                            }
                        }
                    }
                }
            }
        }
    }
    fishDatBuffer[id.x].maxFear = tmpMaxThreat;
    fishDatBuffer[id.x].desiredMateID = tempDesiredID;
}